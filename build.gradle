plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group 'com.mcneilio'
archivesBaseName = 'orcmaker'
version = '1.2.0'

signing {
    def signingKey = System.getenv("GPG_KEY")
    def signingPassword = System.getenv("GPG_PW")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

tasks.register('createProperties') {
    doLast {
        def properties = new Properties()
        properties.setProperty('version', version)
        properties.store(new FileOutputStream(file('version.properties')), null)
    }
}

classes {
    dependsOn 'createProperties'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'OrcMaker',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Josh McNeil',
                'Main-Class': 'com.mcneilio.orcmaker.Main',
                'Class-Path': 'com.mcneilio.orcmaker',
                'Build-Jdk': org.gradle.internal.jvm.Jvm.current(),
                'Gradle-Version': GradleVersion.current().toString()
        )
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "OrcMaker"
                description = "A library for building orc files"
                url = 'https://github.com/mcneiljt/orcmaker'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mcneiljt'
                        name = 'Josh McNeil'
                        email = 'josh@mcneilio.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mcneiljt/orcmaker.git'
                    developerConnection = 'scm:git:ssh://github.com:mcneiljt/orcmaker.git'
                    url = 'https://github.com/mcneiljt/orcmaker'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            credentials {
                username = System.getenv("OSSRH_UN")
                password = System.getenv("OSSRH_PW")
            }
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'org.apache.orc:orc-core:1.8.3'
    implementation 'org.json:json:20211205'
    implementation 'org.apache.hadoop:hadoop-common:3.3.4'
    implementation 'com.datadoghq:java-dogstatsd-client:4.0.0'
    implementation platform('software.amazon.awssdk:bom:2.20.56')
    implementation 'software.amazon.awssdk:s3'
    implementation 'com.google.code.gson:gson'
    implementation 'org.apache.hive:hive-serde'
    implementation 'org.apache.hive:hive-common'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

test {
    useJUnitPlatform()
}